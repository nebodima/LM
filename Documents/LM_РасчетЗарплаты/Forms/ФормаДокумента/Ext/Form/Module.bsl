
&НаКлиенте
Процедура ПериодПриИзменении(Элемент)	
	
	Объект.ДатаНачала = Период.ДатаНачала;
	Объект.ДатаОкончания = Период.ДатаОкончания;
	
	Объект.ДД.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТЧ(Команда)
	Объект.ДД.Очистить();
	ЗаполнитьТЧНаСервере();
КонецПроцедуры

Процедура ЗаполнитьТЧНаСервере()
	
	Запрос = Новый Запрос;
	
	Если Объект.РассчитыватьПоСтавкеПедагога или ЗначениеЗаполнено(Объект.ОплатаСЧеловека) или ЗначениеЗаполнено(Объект.ПроцентСОплатыУченика) Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СписанияСуммЗаУрокиОбороты.ЧасыРасход КАК ЧасыРасход,
		|	СписанияСуммЗаУрокиОбороты.СуммаРасход КАК СуммаРасход,
		|	СписанияСуммЗаУрокиОбороты.Клиент КАК Клиент,
		|	СписанияСуммЗаУрокиОбороты.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.LM_СписанияСуммЗаУроки.Обороты(&НачалоПериода, &КонецПериода, Регистратор, Педагог = &Педагог) КАК СписанияСуммЗаУрокиОбороты
		|ГДЕ
		|	СписанияСуммЗаУрокиОбороты.СуммаОборот <= 0";
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СписанияСуммЗаУрокиОбороты.Регистратор КАК Регистратор,
		|	СписанияСуммЗаУрокиОбороты.ЧасыРасход КАК ЧасыРасход,
		|	СписанияСуммЗаУрокиОбороты.СуммаРасход КАК СуммаРасход,
		|	СписанияСуммЗаУрокиОбороты.Регистратор.Дата КАК РегистраторДата
		|ИЗ
		|	РегистрНакопления.LM_СписанияСуммЗаУроки.Обороты(&НачалоПериода, &КонецПериода, Авто, Педагог = &Педагог) КАК СписанияСуммЗаУрокиОбороты
		|
		|УПОРЯДОЧИТЬ ПО
		|	РегистраторДата";
	КонецЕсли;

	Запрос.УстановитьПараметр("КонецПериода", Объект.ДатаОкончания);
	Запрос.УстановитьПараметр("НачалоПериода", Объект.ДатаНачала);
	Запрос.УстановитьПараметр("Педагог", Объект.Педагог);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Уроки = РезультатЗапроса.Выбрать();
	
	Пока Уроки.Следующий() Цикл		
		
		Если НЕ Объект.ВключаяБесплатныеЗанятия и НЕ ЗначениеЗаполнено(Уроки.СуммаРасход) Тогда 
			Продолжить;
		КонецЕсли;
		
		СтрокаТЧ = Объект.ДД.Добавить();
		Если ЗначениеЗаполнено(Объект.ОплатаСЧеловека) или ЗначениеЗаполнено(Объект.ПроцентСОплатыУченика)
			или (Объект.РассчитыватьПоСтавкеПедагога и ЗначениеЗаполнено(Уроки.Регистратор.СтавкаПедагога.ОплатаСЧеловека))
			или (Объект.РассчитыватьПоСтавкеПедагога и ЗначениеЗаполнено(Уроки.Регистратор.СтавкаПедагога.ПроцентСОплатыУченика)) Тогда
			СтрокаТЧ.Контрагент = Уроки.Клиент;
			СтрокаТЧ.Документ 	= Уроки.Регистратор;
		Иначе
			СтрокаТЧ.Документ 	= Уроки.Регистратор;
		КонецЕсли;
		СтрокаТЧ.Сумма 		= Уроки.СуммаРасход;
		СтрокаТЧ.Часы 		= '00010101'+Уроки.ЧасыРасход;
		
		Если Объект.РассчитыватьПоСтавкеПедагога Тогда
			
			Если ЗначениеЗаполнено(Уроки.Регистратор.СтавкаПедагога) Тогда
				Если ЗначениеЗаполнено(Уроки.Регистратор.СтавкаПедагога.ОплатаЗаЧас) Тогда
					СтрокаТЧ.Начисления = Уроки.Регистратор.СтавкаПедагога.ОплатаЗаЧас * (Уроки.ЧасыРасход/60/60);
				ИначеЕсли ЗначениеЗаполнено(Уроки.Регистратор.СтавкаПедагога.ПроцентСОплатыУченика) Тогда
					СтрокаТЧ.Начисления = Уроки.СуммаРасход * Уроки.Регистратор.СтавкаПедагога.ПроцентСОплатыУченика / 100;
				ИначеЕсли ЗначениеЗаполнено(Уроки.Регистратор.СтавкаПедагога.ОплатаЗаЗанятие) Тогда
					СтрокаТЧ.Начисления = Уроки.Регистратор.СтавкаПедагога.ОплатаЗаЗанятие;
				ИначеЕсли ЗначениеЗаполнено(Уроки.Регистратор.СтавкаПедагога.ОплатаСЧеловека) Тогда
					СтрокаТЧ.Начисления = Уроки.Регистратор.СтавкаПедагога.ОплатаСЧеловека;
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			
			Если ЗначениеЗаполнено(Объект.ОплатаЗаЧас) Тогда
				СтрокаТЧ.Начисления = Объект.ОплатаЗаЧас * (Уроки.ЧасыРасход/60/60);
			ИначеЕсли ЗначениеЗаполнено(Объект.ПроцентСОплатыУченика) Тогда
				СтрокаТЧ.Начисления = Уроки.СуммаРасход * Объект.ПроцентСОплатыУченика / 100;
			ИначеЕсли ЗначениеЗаполнено(Объект.ОплатаЗаЗанятие) Тогда
				СтрокаТЧ.Начисления = Объект.ОплатаЗаЗанятие;
			ИначеЕсли ЗначениеЗаполнено(Объект.ОплатаСЧеловека) Тогда
				СтрокаТЧ.Начисления = Объект.ОплатаСЧеловека;
			КонецЕсли;
		КонецЕсли;
	
	КонецЦикла;
	
	Объект.ДопРасходы 	  	= ПолучитьДопРасходы();		
	Объект.СуммаДокумента 	= Объект.ДД.Итог("Начисления") - Объект.ДопРасходы;
	
КонецПроцедуры

Функция ПолучитьДопРасходы()
	
	Если ЗначениеЗаполнено(Объект.Касса) и ЗначениеЗаполнено(Объект.ДатаОкончания) и ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КассаОбороты.СуммаРасход КАК СуммаРасход
		|ИЗ
		|	РегистрНакопления.LM_Касса.Обороты(&НачалоПериода, &КонецПериода, , Касса = &Касса) КАК КассаОбороты";
		Запрос.УстановитьПараметр("КонецПериода", Объект.ДатаОкончания);
		Запрос.УстановитьПараметр("НачалоПериода", Объект.ДатаНачала);
		Запрос.УстановитьПараметр("Касса", Объект.Касса);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Возврат ВыборкаДетальныеЗаписи.СуммаРасход;
		КонецЦикла;
		Возврат 0;
	Иначе
		Возврат 0;
	КонецЕсли;	
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Период.ДатаНачала = Объект.ДатаНачала;
	Период.ДатаОкончания = Объект.ДатаОкончания;
	
	ОстатокВКассе = КассаПриИзмененииНаСервере();
	
	ОбновитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ПедагогПриИзменении(Элемент) 	
	Объект.ДД.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ДДПриИзменении(Элемент)
	Объект.СуммаДокумента 		= Объект.ДД.Итог("Начисления");
КонецПроцедуры

Функция КассаПриИзмененииНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КассаОстатки.СуммаОстаток КАК СуммаОстаток
		|ИЗ
		|	РегистрНакопления.LM_Касса.Остатки(, ) КАК КассаОстатки
		|ГДЕ
		|	КассаОстатки.Касса = &Касса";  	
	Запрос.УстановитьПараметр("Касса", Объект.Касса);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.СуммаОстаток;
	КонецЦикла;
	
	Возврат 0;
	
КонецФункции

&НаКлиенте
Процедура КассаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда
		ОстатокВКассе = КассаПриИзмененииНаСервере();
		Иначе
		ОстатокВКассе = 0;

	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьОстатокПоКассе(Команда)
	ОстатокВКассе = КассаПриИзмененииНаСервере();	
КонецПроцедуры

Функция ПереместитьДеньгиНаСервере()
	
	НовПеремещение = Документы.LM_ПеремещениеДенег.СоздатьДокумент();
	НовПеремещение.ВидОперации 			= Перечисления.LM_ВидыОпераций.ВнутренняяКасса;
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		НовПеремещение.ДокументОснования 	= Объект.Ссылка;
	КонецЕсли;

	НовПеремещение.АвторДокумента 		= Пользователи.АвторизованныйПользователь();
	НовПеремещение.КассаОтправитель		= Объект.Касса;
	НовПеремещение.КассаПолучатель		= Справочники.Кассы.ПолучитьОсновнуюКассу();
	НовПеремещение.Клиент 				= Объект.Педагог;
	НовПеремещение.Сумма 				= КассаПриИзмененииНаСервере();
	НовПеремещение.Дата					= Объект.ДатаОкончания-1;
	НовПеремещение.Организация 			= Объект.Организация;
	
	Попытка
		НовПеремещение.Записать(РежимЗаписиДокумента.Проведение);
		Возврат НовПеремещение.Ссылка;
	Исключение
		Сообщить("Не удалось провести Перемещение!"+ " " +ОписаниеОшибки());
	КонецПопытки;
	
	Возврат Документы.LM_ПеремещениеДенег.ПустаяСсылка();
	
КонецФункции

&НаКлиенте
Процедура ПереместитьДеньги(Команда)
	               	
	Если ЗначениеЗаполнено(Объект.Пермещение) Тогда
		Сообщить("Невозможно создать еще одно Перемещение! Существует уже созданное ранее.");
		Возврат;
	КонецЕсли; 	
	
	Если ЗначениеЗаполнено(Объект.СуммаДокумента) и ЗначениеЗаполнено(Объект.Касса) и ЗначениеЗаполнено(Объект.Педагог) и ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда
		Объект.Пермещение = ПереместитьДеньгиНаСервере();
		//ОткрытьЗначение(Объект.Пермещение);
	Иначе
		Сообщить("Невозможно создать Перемещение! Возможно незаполнены поля: Касса, Педагог, СуммаДокумента, Период");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СуммаДокументаПриИзменении(Элемент)
	Объект.СуммаДокумента = Объект.ДД.Итог("Начисления") - Объект.ДопРасходы;
	Объект.ПроцентСОплатыУченика = 0;
КонецПроцедуры

Функция СделтьРКОНаСервере()
	
	НовРКО = Документы.РасходныйКассовыйОрдер.СоздатьДокумент();
	НовРКО.ВидОперации 			= Перечисления.LM_ВидыОпераций.ВнутренняяКасса;
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		НовРКО.ДокументОснования 	= Объект.Ссылка;
	КонецЕсли;
	НовРКО.АвторДокумента 		= Пользователи.АвторизованныйПользователь();
	НовРКО.Касса 				= Объект.Касса;
	НовРКО.Клиент 				= Объект.Педагог;
	НовРКО.Организация 			= Объект.Организация;
	НовРКО.Сумма 				= Объект.СуммаДокумента;
	НовРКО.Дата					= Объект.ДатаОкончания-1;
	
	Попытка
		НовРКО.Записать(РежимЗаписиДокумента.Проведение);
		Возврат НовРКО.Ссылка;
	Исключение
		Сообщить("Не удалось провести РКО!"+ " " +ОписаниеОшибки());
	КонецПопытки;

	Возврат Документы.РасходныйКассовыйОрдер.ПустаяСсылка();
	
КонецФункции

&НаКлиенте
Процедура СделтьРКО(Команда)
	
	Если ЗначениеЗаполнено(Объект.РКО) Тогда
		Сообщить("Невозможно создать еще один Расходный ордер! Существует уже созданный ранее.");
		Возврат;
	КонецЕсли; 	
	
	Если ЗначениеЗаполнено(Объект.СуммаДокумента) и ЗначениеЗаполнено(Объект.Касса) и ЗначениеЗаполнено(Объект.Педагог) и ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда
		Объект.РКО = СделтьРКОНаСервере();
		//ОткрытьЗначение(Объект.РКО);
	Иначе
		Сообщить("РКО невозможно создать! Возможно незаполнены поля: Касса, Педагог, СуммаДокумента, Период");
	КонецЕсли;

КонецПроцедуры

Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);	
КонецПроцедуры

&НаКлиенте
Процедура ПроцентОтОплатыПриИзменении(Элемент)
	Объект.ОплатаЗаЧас	   = 0;
	Объект.ОплатаСЧеловека = 0;
	Объект.ОплатаЗаЗанятие = 0;
КонецПроцедуры

Процедура ОбновитьВидимость()
	
	Если Объект.РассчитыватьПоСтавкеПедагога Тогда
		Элементы.ПроцентСОплатыУченика.Доступность 	= Ложь;
		Элементы.ОплатаЗаЗанятие.Доступность 		= Ложь;
		Элементы.ОплатаЗаЧас.Доступность 			= Ложь;
		Элементы.ОплатаСЧеловека.Доступность 		= Ложь;
	Иначе
		Элементы.ПроцентСОплатыУченика.Доступность 	= Истина;
		Элементы.ОплатаЗаЗанятие.Доступность 		= Истина;
		Элементы.ОплатаЗаЧас.Доступность 			= Истина;
		Элементы.ОплатаСЧеловека.Доступность 		= Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаПедагогаПриИзменении(Элемент)
	ОбновитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура ОплатаЗаЗанятиеПриИзменении(Элемент)
	Объект.ОплатаЗаЧас			 = 0;
	Объект.ОплатаСЧеловека       = 0;
	Объект.ПроцентСОплатыУченика = 0;
КонецПроцедуры

&НаКлиенте
Процедура ОплатаСЧеловекаПриИзменении(Элемент)
	Объект.ОплатаЗаЧас			 = 0;
	Объект.ОплатаЗаЗанятие       = 0;
	Объект.ПроцентСОплатыУченика = 0;
КонецПроцедуры

&НаКлиенте
Процедура ОплатаЗаЧасПриИзменении(Элемент)
	Объект.ОплатаЗаЗанятие			 = 0;
	Объект.ОплатаСЧеловека       = 0;
	Объект.ПроцентСОплатыУченика = 0;
КонецПроцедуры


