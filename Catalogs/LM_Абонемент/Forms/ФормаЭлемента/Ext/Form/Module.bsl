
&НаКлиенте
Процедура Активировать(Команда)
	
	Объект.Активный = НЕ Объект.Активный;
	
	Если Объект.Активный Тогда
		Объект.ДатаАктивации = ТекущаяДата();	
	КонецЕсли;
	
	ОбновитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура Приостановить(Команда)
	
	Если Объект.Активный Тогда
		ОП = Новый ОписаниеОповещения("ЗапросДнейПриостановки",ЭтотОбъект);
		ОткрытьФорму("Справочник.LM_Абонемент.Форма.ФормаПриостановки",,ЭтотОбъект,,,,ОП,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
	ОбновитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапросДнейПриостановки(Результат,Параметры) Экспорт 
	
	Если ЗначениеЗаполнено(Результат.ПериодПриостановки) Тогда
		
		ПериодПриостановки = Результат.ПериодПриостановки;
		
		Если Объект.ДатаНачала > ПериодПриостановки.ДатаНачала или Объект.ДатаОкончания <= КонецДня(ПериодПриостановки.ДатаОкончания)+1
			или ПериодПриостановки.ДатаНачала >= КонецДня(ПериодПриостановки.ДатаОкончания)+1 Тогда
			Сообщить("Ошибка в датах приостановок!");
			Возврат;
		КонецЕсли;         	
		
		НовСтр = Объект.Приостановки.Добавить();
		
		НовСтр.ДатаИзменения 		= ТекущаяДата();
		НовСтр.ДатаПриостановки 	= ПериодПриостановки.ДатаНачала;
		НовСтр.ДатаВозобновления 	= КонецДня(ПериодПриостановки.ДатаОкончания)+1;
		НовСтр.Комментарий 			= Результат.Причина;
		
		Попытка
			Записать();
		Исключение
			Сообщить(""+ОписаниеОшибки())
		КонецПопытки;
		
	КонецЕсли;
	
	ОбновитьВидимость();
	
КонецПроцедуры 

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ Объект.Ссылка.Пустая() Тогда
		ЗаполнитьТЗОплат();
	КонецЕсли;	
	
	ОбновитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимость()
	
	Если ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		Элементы.Период.ТолькоПросмотр = Истина;
	Иначе
		Элементы.Период.ТолькоПросмотр = Ложь;;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ЦенаАбонемента) Тогда
		Элементы.ЦенаАбонемента.ТолькоПросмотр = Истина;
	Иначе
		Элементы.ЦенаАбонемента.ТолькоПросмотр = Ложь;;
	КонецЕсли;	
	
	Период.ДатаНачала 	 = Объект.ДатаНачала;
	Период.ДатаОкончания = Объект.ДатаОкончания;
	
	Если Объект.Активный Тогда
		
		Элементы.Приостановить.Доступность 		= Истина;
		
		Элементы.Активировать.Заголовок 		= "Сделать неактивным";
		
		СтруктураПриостановок = НайтиПриостановки();
		
		Если СтруктураПриостановок <> Неопределено Тогда
			Элементы.Группа8.Доступность 			= Истина;
			//Элементы.Приостановить.Заголовок 		= "Возобновить";
			Элементы.ДекорацияСтатус.Заголовок 		= "Приостановлен до " +Формат(СтруктураПриостановок.ДатаВозобновления,"ДФ=dd.MM.yyyy");
			Элементы.ДекорацияСтатус.ЦветФона 		= WebЦвета.Циан;
		Иначе
			Элементы.Группа8.Доступность 			= Ложь;
			Элементы.ДекорацияСтатус.Заголовок 		= "Активен на " +ТекущаяДата();
			Элементы.ДекорацияСтатус.ЦветФона 		= WebЦвета.СветлоЗеленый;
			//Элементы.Приостановить.Заголовок 		= "Приостановить";
		КонецЕсли;
	Иначе
		Элементы.Активировать.Заголовок 		= "Активировать";
		Элементы.ДекорацияСтатус.Заголовок 		= "Неактивен";
		Элементы.ДекорацияСтатус.ЦветФона 		= WebЦвета.СветлоСерый;
		Элементы.Приостановить.Доступность 		= Ложь;
	КонецЕсли;
	
	ПоказатьФото();
	
КонецПроцедуры

Функция НайтиПриостановки()
	
	ТекДата = ТекущаяДата();
	
	Для Каждого СтрПриостановка из Объект.Приостановки Цикл
		
		Если ТекДата > СтрПриостановка.ДатаПриостановки и ТекДата < СтрПриостановка.ДатаВозобновления Тогда
			С = Новый Структура;
			С.Вставить("ДатаПриостановки",СтрПриостановка.ДатаПриостановки);
			С.Вставить("ДатаВозобновления",СтрПриостановка.ДатаВозобновления);
			Возврат С;
		КонецЕсли;
		
	КонецЦикла;
		
	Возврат Неопределено;
	
КонецФункции

Процедура ЗаполнитьТЗОплат()
	
	Оплачено 		= 0;
	Израсходовано 	= 0;
	Остаток 		= 0;
	ИтогоЧасов 		= 0;
	Элементы.Декорация3.Заголовок = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	LM_СписанияСуммЗаУрокиОстаткиИОбороты.СуммаКонечныйОстаток КАК Остаток,
		|	LM_СписанияСуммЗаУрокиОстаткиИОбороты.Регистратор КАК Документ,
		|	LM_СписанияСуммЗаУрокиОстаткиИОбороты.ПериодСекунда КАК Дата,
		|	LM_СписанияСуммЗаУрокиОстаткиИОбороты.СуммаОборот КАК Сумма,
		|	LM_СписанияСуммЗаУрокиОстаткиИОбороты.Клиент КАК Ученик,
		|	LM_СписанияСуммЗаУрокиОстаткиИОбороты.СуммаПриход КАК СуммаПриход,
		|	LM_СписанияСуммЗаУрокиОстаткиИОбороты.СуммаРасход КАК СуммаРасход,
		|	LM_СписанияСуммЗаУрокиОстаткиИОбороты.ЧасыРасход / 60 / 60 КАК ЧасыРасход,
		|	LM_СписанияСуммЗаУрокиОстаткиИОбороты.КоличествоЯвокОборот КАК Явок,
		|	LM_СписанияСуммЗаУрокиОстаткиИОбороты.КоличествоУроковОборот КАК Уроков,
		|	ВЫБОР
		|		КОГДА LM_СписанияСуммЗаУрокиОстаткиИОбороты.КоличествоЯвокОборот <> 0
		|			ТОГДА LM_СписанияСуммЗаУрокиОстаткиИОбороты.ЧасыРасход / 60 / 60
		|	КОНЕЦ КАК ЧасыРасходПоЯвкам
		|ИЗ
		|	РегистрНакопления.LM_СписанияСуммЗаУроки.ОстаткиИОбороты(, , Авто, , Абонемент = &Абонемент) КАК LM_СписанияСуммЗаУрокиОстаткиИОбороты
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ученик,
		|	Дата
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Абонемент", Объект.Ссылка);
	//Запрос.УстановитьПараметр("ДатаНачала", '00010101');  //Объект.ДатаНачала
	//Запрос.УстановитьПараметр("ДатаОкончания", '20990101'); //Объект.ДатаОкончания
	
	Попытка
		ВремТЗ = Запрос.Выполнить().Выгрузить();
		
		Если ВремТЗ.Количество()<> 0 Тогда		    
			Оплачено 		= ВремТЗ.Итог("СуммаПриход"); //ВремТЗ[ВремТЗ.Количество()-1].Остаток;
			Израсходовано 	= ВремТЗ.Итог("СуммаРасход");
			Остаток 		= Оплачено - Израсходовано;
			ИтогоЧасов 		= ВремТЗ.Итог("ЧасыРасход");
			ИтогоУроков		= ВремТЗ.Итог("Уроков");
			ИтогоЯвок		= ВремТЗ.Итог("Явок");
			ИтогоЧасовПоЯвкам = ВремТЗ.Итог("ЧасыРасходПоЯвкам");
			
	        ОстатокУроковЧасов = ОстатокУроковЧасов();
			
			Если ЗначениеЗаполнено(Объект.ЦенаАбонемента) и Оплачено >= Объект.ЦенаАбонемента Тогда
				Элементы.Декорация3.Заголовок = "(Оплачен полностью)";
				Элементы.Декорация3.ЦветТекста = WebЦвета.ЗеленыйЛес;
			ИначеЕсли ЗначениеЗаполнено(Объект.ЦенаАбонемента) и Оплачено < Объект.ЦенаАбонемента Тогда
				Элементы.Декорация3.Заголовок = "(Осталось оплатить " +Формат(Объект.ЦенаАбонемента - Оплачено,"ЧДЦ=2")+")";
				Элементы.Декорация3.ЦветТекста = WebЦвета.СветлоСерый;
			КонецЕсли;
			
		КонецЕсли;  	
		
		ТЗОплат.Загрузить(ВремТЗ);
		
	Исключение
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция ОстатокУроковЧасов()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) и Объект.КоличествоЗанятий > 0 и Остаток > 0 Тогда 
		Возврат Остаток / (Объект.ЦенаАбонемента / Объект.КоличествоЗанятий);
	Иначе 
		Возврат 0;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьФизЛицо(Команда)
	Элементы.ФизЛицо.ТолькоПросмотр = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	Объект.ДатаНачала = Период.ДатаНачала;
	Объект.ДатаОкончания = Период.ДатаОкончания;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПериод(Команда)
	Элементы.Период.ТолькоПросмотр = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОплаченоНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = ОткрытьВзаиморасчетыНаСервере(Объект.Ссылка);
	ОткрытьФорму("Отчет.LM_Взаиморасчеты.Форма", ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Функция ОткрытьВзаиморасчетыНаСервере(Значение)
	
	КомпоновщикНастроек = Отчеты.LM_Взаиморасчеты.Создать().КомпоновщикНастроек; 
	Настройки 			= КомпоновщикНастроек.Настройки; 
	
	ЭлементНастройки = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период")); 
	Если ЗначениеЗаполнено(ЭлементНастройки.ИдентификаторПользовательскойНастройки) Тогда 
		ПользовательскийПараметр = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ЭлементНастройки.ИдентификаторПользовательскойНастройки); 
		Если ТипЗнч(ПользовательскийПараметр) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда 
			ПользовательскийПараметр.Использование = Ложь;
		КонецЕсли; 
	КонецЕсли; 
	
	ОтборАбонемент = Новый ПолеКомпоновкиДанных("Абонемент");
	
	Для Каждого ЭлементНастройки Из Настройки.Отбор.Элементы Цикл 
		Если ЭлементНастройки.ЛевоеЗначение = ОтборАбонемент Тогда 
			ЭлементНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно; 
			ЭлементНастройки.ПравоеЗначение = Значение; 
			ЭлементНастройки.Использование = Истина; 
			Если ЗначениеЗаполнено(ЭлементНастройки.ИдентификаторПользовательскойНастройки) Тогда 
				ПользовательскийПараметр = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ЭлементНастройки.ИдентификаторПользовательскойНастройки); 
				Если ТипЗнч(ПользовательскийПараметр) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда 
					ПользовательскийПараметр.ВидСравнения = ЭлементНастройки.ВидСравнения; 
					ПользовательскийПараметр.ПравоеЗначение = ЭлементНастройки.ПравоеЗначение; 
					ПользовательскийПараметр.Использование = ЭлементНастройки.Использование; 
				КонецЕсли; 
			КонецЕсли;
			Прервать; 
		КонецЕсли; 
	КонецЦикла; 
	
	ПараметрыОткрытия = Новый Структура(); 
	ПараметрыОткрытия.Вставить("СформироватьПриОткрытии", Истина); 
	ПараметрыОткрытия.Вставить("Вариант", Настройки); 
	ПараметрыОткрытия.Вставить("ПользовательскиеНастройки", КомпоновщикНастроек.ПользовательскиеНастройки); 
	
	Возврат ПараметрыОткрытия;
	
КонецФункции

&НаКлиенте
Процедура ФизЛицоПриИзменении(Элемент)
	ПоказатьФото();
КонецПроцедуры

&НаСервере
Процедура ПоказатьФото()
	Если НЕ Объект.ФизЛицо.Пустая() и НЕ Объект.ФизЛицо.Фото.Пустая() Тогда
		Фото = ПоместитьВоВременноеХранилище(Объект.ФизЛицо.Фото.Файл.Получить());
	Иначе
		Фото = "";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЦенуАбонемента(Команда)
	Элементы.ЦенаАбонемента.ТолькоПросмотр = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПриостановкиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ОбновитьВидимость();
КонецПроцедуры


&НаКлиенте
Процедура ИтогоПосещенийНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыОткрытия = ОткрытьВзаиморасчетыНаСервере(Объект.Ссылка);
	ОткрытьФорму("Отчет.LM_Взаиморасчеты.Форма", ПараметрыОткрытия, ЭтаФорма);

КонецПроцедуры

